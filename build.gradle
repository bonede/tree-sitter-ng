
import org.treesitter.build.GenTask
import java.net.http.HttpClient
import java.net.http.HttpRequest
import java.net.http.HttpResponse

plugins {
    alias(libs.plugins.download)
    alias(libs.plugins.nexus.staging)
    id 'base'
}

allprojects {
    group = 'io.github.bonede'
    repositories {
        mavenCentral()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}


subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation platform(libs.junit.bom)
        testImplementation 'org.junit.jupiter:junit-jupiter'
        if(project.name != 'tree-sitter'){
            testImplementation project(":tree-sitter-tests")
            implementation project(":tree-sitter")
        }
    }

    publishing {
        repositories {
            maven {
                name = "MavenCentral"
                def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl =  "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"
                credentials {
                    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = project.name
                    url = 'https://github.com/bonede/tree-sitter-ng'
                    description = "Next generation Tree Sitter Java binding"
                    licenses {
                        license {
                            name = 'MIT'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                        developerConnection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                        url = 'https://github.com/bonede/tree-sitter-ng'
                    }
                    developers {
                        developer {
                            id = 'bonede'
                            name = 'Wang Liang'
                            email = 'bonede@qq.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        if (project.hasProperty('signing.keyId')) {
            sign configurations.archives
            sign publishing.publications
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('downloadSource', DownloadSourceTask)

    tasks.register('buildNative', BuildNativeTask) {
        zigExe = rootProject.downloadZig.zigExe
        dependsOn downloadSource, rootProject.downloadZig
    }

    tasks.withType(Javadoc) {
        options.locale = 'en_US'
        options.JFlags = ["-Duser.language=en_US"]
    }
}


nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    packageGroup = "io.github.bonede"
    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
        username = ossrhUsername
        password = ossrhPassword
    }
}

tasks.register('downloadMiniSign', DownloadMiniSignTask)

tasks.register('downloadZig', DownloadZigTask){
    miniSignExe = downloadMiniSign.miniSignExe
    dependsOn downloadMiniSign
}

tasks.register("gen", GenTask) {
    group = "build setup"
    description = "Generate sub project for tree sitter parser"
}

tasks.register("publishPortal") {
    // Use this task after :publish to make package visible in Portal
    // Reference https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#ensuring-deployment-visibility-in-the-central-publisher-portal
    doLast {
        def url = "https://ossrh-staging-api.central.sonatype.com/22manual/upload/defaultRepository/io.github.bonede"
        def token = Base64.encoder.encodeToString("$ossrhUsername:$ossrhPassword".getBytes())
        def client = HttpClient.newHttpClient()
        def req = HttpRequest.newBuilder()
                .uri(URI.create(url))
                .header("Authorization", "Bearer $token")
                .POST(HttpRequest.BodyPublishers.noBody())
                .build()
        def resp = client.send(req, HttpResponse.BodyHandlers.ofString())
        def code = resp.statusCode();
        if(resp.statusCode() != 200){
            def msg = "HTTP request failed with status $code"
            System.err.println(msg)
            throw new GradleException(msg)
        }
        println "Published to Portal with response body:\n${resp.body()}"
    }
}