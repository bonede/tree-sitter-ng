
import org.treesitter.build.GenTask
import java.net.http.HttpClient
import java.net.http.HttpRequest
import java.net.http.HttpResponse

plugins {
    alias(libs.plugins.download)
    alias(libs.plugins.nexus.staging)
    id 'base'
}

allprojects {
    group = 'io.github.bonede'
    repositories {
        mavenCentral()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}


subprojects {
    if (project.hasProperty('libVersion')) {
        version = project.libVersion
    }

    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation platform(libs.junit.bom)
        testImplementation 'org.junit.jupiter:junit-jupiter'
        if(project.name != 'tree-sitter'){
            testImplementation project(":tree-sitter-tests")
            implementation project(":tree-sitter")
        }
    }

    publishing {
        repositories {
            maven {
                name = "MavenCentral"
                def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl =  "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"
                credentials {
                    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = project.name
                    url = 'https://github.com/bonede/tree-sitter-ng'
                    description = "Next generation Tree Sitter Java binding"
                    licenses {
                        license {
                            name = 'MIT'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                        developerConnection = 'scm:git:https://github.com/bonede/tree-sitter-ng.git'
                        url = 'https://github.com/bonede/tree-sitter-ng'
                    }
                    developers {
                        developer {
                            id = 'bonede'
                            name = 'Wang Liang'
                            email = 'bonede@qq.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        if (project.hasProperty('signing.keyId')) {
            sign configurations.archives
            sign publishing.publications
        }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('downloadSource', DownloadSourceTask)

    if (project.name != 'tree-sitter' && project.name != 'tree-sitter-demo' && project.name != 'tree-sitter-tests') {
        tasks.named('test') {
            dependsOn tasks.named('downloadSource')
        }
    }

    tasks.register('buildNative', BuildNativeTask) {
        zigExe = rootProject.downloadZig.zigExe
        dependsOn downloadSource, rootProject.downloadZig
    }

    tasks.withType(Javadoc) {
        options.locale = 'en_US'
        options.JFlags = ["-Duser.language=en_US"]
    }
}


nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    packageGroup = "io.github.bonede"
    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
        username = ossrhUsername
        password = ossrhPassword
    }
}

tasks.register('downloadMiniSign', DownloadMiniSignTask)

tasks.register('downloadZig', DownloadZigTask){
    miniSignExe = downloadMiniSign.miniSignExe
    dependsOn downloadMiniSign
}

tasks.register("gen", GenTask) {
    group = "build setup"
    description = "Generate sub project for tree sitter parser"
}

tasks.register("dropStagingRepo") {
    group = "publishing"
    description = "Drop closed staging repositories to unblock new releases"
    doLast {
        def baseUrl = "https://ossrh-staging-api.central.sonatype.com"
        def token = Base64.encoder.encodeToString("$ossrhUsername:$ossrhPassword".getBytes())
        def client = HttpClient.newHttpClient()

        // Search for closed repositories
        def searchUrl = "$baseUrl/manual/search/repositories?profile_id=io.github.bonede&state=closed&ip=any"
        def searchReq = HttpRequest.newBuilder()
                .uri(URI.create(searchUrl))
                .header("Authorization", "Bearer $token")
                .GET()
                .build()
        def searchResp = client.send(searchReq, HttpResponse.BodyHandlers.ofString())
        if (searchResp.statusCode() != 200) {
            throw new GradleException("Search failed with status ${searchResp.statusCode()}: ${searchResp.body()}")
        }
        println "Found repositories:\n${searchResp.body()}"

        // Parse repository keys and drop each one
        def json = new groovy.json.JsonSlurper().parseText(searchResp.body())
        def repos = json.repositories ?: []
        if (repos.isEmpty()) {
            println "No closed staging repositories found."
            return
        }
        repos.each { repo ->
            def key = repo.key
            println "Dropping repository: $key (state: ${repo.state})"
            def dropReq = HttpRequest.newBuilder()
                    .uri(URI.create("$baseUrl/manual/drop/repository/$key"))
                    .header("Authorization", "Bearer $token")
                    .DELETE()
                    .build()
            def dropResp = client.send(dropReq, HttpResponse.BodyHandlers.ofString())
            if (dropResp.statusCode() != 200 && dropResp.statusCode() != 204) {
                System.err.println("Drop failed for $key with status ${dropResp.statusCode()}: ${dropResp.body()}")
            } else {
                println "Dropped $key successfully."
            }
        }
    }
}

tasks.register("postPublish") {
    group = "publishing"
    description = "Upload staging deployment to Central Portal. Drops closed repos if needed."
    doLast {
        def baseUrl = "https://ossrh-staging-api.central.sonatype.com"
        def namespace = "io.github.bonede"
        def token = Base64.encoder.encodeToString("$ossrhUsername:$ossrhPassword".getBytes())
        def client = HttpClient.newHttpClient()

        def uploadUrl = "$baseUrl/manual/upload/defaultRepository/$namespace"
        def req = HttpRequest.newBuilder()
                .uri(URI.create(uploadUrl))
                .header("Authorization", "Bearer $token")
                .POST(HttpRequest.BodyPublishers.noBody())
                .build()
        def resp = client.send(req, HttpResponse.BodyHandlers.ofString())

        if (resp.statusCode() == 400 && resp.body()?.contains("must be dropped")) {
            println "Detected stale closed repository, dropping it..."

            // Search and drop closed repos
            def searchUrl = "$baseUrl/manual/search/repositories?profile_id=$namespace&state=closed&ip=any"
            def searchReq = HttpRequest.newBuilder()
                    .uri(URI.create(searchUrl))
                    .header("Authorization", "Bearer $token")
                    .GET()
                    .build()
            def searchResp = client.send(searchReq, HttpResponse.BodyHandlers.ofString())
            if (searchResp.statusCode() == 200) {
                def json = new groovy.json.JsonSlurper().parseText(searchResp.body())
                (json.repositories ?: []).each { repo ->
                    println "Dropping ${repo.key}..."
                    def dropReq = HttpRequest.newBuilder()
                            .uri(URI.create("$baseUrl/manual/drop/repository/${repo.key}"))
                            .header("Authorization", "Bearer $token")
                            .DELETE()
                            .build()
                    client.send(dropReq, HttpResponse.BodyHandlers.ofString())
                }
            }

            println "Stale repositories dropped. Please re-run publish and then postPublish:"
            println "  ./gradlew :tree-sitter-<package>:publish"
            println "  ./gradlew postPublish"
            return
        }

        if (resp.statusCode() == 400 && resp.body()?.contains("No repository found")) {
            println "No staging repository found. Please publish first:"
            println "  ./gradlew :tree-sitter-<package>:publish"
            return
        }

        if (resp.statusCode() != 200) {
            def msg = "HTTP request failed with status ${resp.statusCode()}"
            System.err.println(msg)
            System.err.println(resp.body())
            throw new GradleException(msg)
        }
        println "Published to Portal with response body:\n${resp.body()}"
    }
}